<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet version="1.0"
xmlns:xsl="http://www.w3.org/1999/XSL/Transform">
  <xsl:output method="html" version="5.0" encoding="UTF-8" indent="yes"/>
  <!-- ========================= -->
  <!-- Calculations    -->
  <!-- ========================= -->
  <xsl:variable name="nbactions" select="count(//ats/actions/action)"/>
  <xsl:variable name="nbpassed" select="count(//ats/actions/action[passed = 'true'])"/>
  <xsl:variable name="nbwarning" select="count(//ats/actions/action[passed = 'warning'])"/>
  <xsl:variable name="nbko" select="count(//ats/actions/action[passed = 'false'])"/>  
  <!-- ========================= -->
  <!-- Appel du template: ats -->
  <!-- ========================= -->
  <xsl:template match="ats">
    <HTML LANG="en">
      <HEAD>
	<META charset="utf-8"/>
	<META name="viewport" content="width=device-width, initial-scale=1, shrink-to-fit=no"/>
	<META name="description" content=""/>
	<TITLE>Agilitest test reporting</TITLE>
	<LINK rel="stylesheet" href="https://stackpath.bootstrapcdn.com/bootstrap/4.4.1/css/bootstrap.min.css" integrity="sha384-Vkoo8x4CGsO3+Hhxv8T/Q5PaXtkKtu6ug5TOeNV6gBiFeWPGFN9MuhOf23Q9Ifjh" crossorigin="anonymous"/>
	<LINK rel="stylesheet" href="report.css"/>
      </HEAD>
      <BODY class="py-4">
	<DIV class="container">
	  <xsl:apply-templates/>
	</DIV>
      </BODY>
    </HTML>
  </xsl:template>
  <!-- ========================= -->
  <!-- child element: script     -->
  <!-- ========================= -->
  <xsl:template match="script">
    <TABLE class="execution table table-borderless">
      <TBODY> 
	<TR>
	  <TH class="align-middle text-center">
            <IMG src="agilitest.png"/>
	  </TH>
	  <TH class="align-middle text-center">
            <H1>
	      ActionTestScript - Test Result
	    </H1>
	  </TH>
	</TR>
      </TBODY>
    </TABLE>
    <TABLE class="execution table table-bordered table-hover">
      <TBODY>
	<TR>
	  <TD class="w-75 p-3" COLSPAN="2">
	    <H3>
              <xsl:value-of select="@testName"/>
	    </H3>
          </TD>
	  <TD class="align-middle text-center">
	    <xsl:text disable-output-escaping="yes">&lt;A target=&quot;_blank&quot; href=&quot;</xsl:text>
	    <xsl:value-of select="concat(@testName,'_xml.pdf')"/>
	    <xsl:text disable-output-escaping="yes">&quot; class=&quot;btn btn-secondary btn-sm &quot; role=&quot;button&quot; aria-pressed=&quot;true&quot;&gt;PDF report&lt;/a&gt;</xsl:text>
	  </TD>

        </TR>
	<TR>
	  <TD class="w-25 p-3">
            Id : 
          </TD>
	  <TD COLSPAN="2">
            <xsl:value-of select="@testId"/>
          </TD>
        </TR>
	<TR>
	  <TD class="w-25 p-3">
            Description :
          </TD>
	  <TD COLSPAN="2">
	    <xsl:value-of select="description"/>
          </TD>
        </TR>
	<TR>
	  <TD class="w-25 p-3">
            Prerequisite :
          </TD>
	  <TD COLSPAN="2">
            <xsl:value-of select="prerequisite"/>
          </TD>
        </TR>
	<TR>
	  <TD class="w-25 p-3">
            Author :
          </TD>
	  <TD COLSPAN="2">
            <xsl:value-of select="author"/>
          </TD>
        </TR>
	<TR>
	  <TD class="w-25 p-3">
            Executed :
          </TD>
	  <TD COLSPAN="2">
            <xsl:value-of select="executed"/>
          </TD>
        </TR>
	<TR>
	  <TD class="w-25 p-3">
            Groups :
          </TD>
	  <TD COLSPAN="2">
            <xsl:apply-templates select="groups"/>
          </TD>
        </TR>
	<xsl:apply-templates select="test"/>
      </TBODY>
    </TABLE>
    <TABLE class="result table table-bordered table-hover">
      <THEAD>
	<TR>
	  <xsl:choose>
            <xsl:when test="$nbko &gt; 0">
	      <TD class="test-failed" COLSPAN="4" STYLE="text-align: center;">
                <IMG src="false.png"/>
	      </TD>
            </xsl:when>
            <xsl:when test="$nbko = 0">
	      <TD class="test-passed" COLSPAN="4" STYLE="text-align: center;">
                <IMG src="true.png"/>
	      </TD>
	    </xsl:when>
          </xsl:choose>
        </TR>
      </THEAD>
      <TBODY>
	<TR>
	  <TD>
            Nb actions :
            <xsl:value-of select="$nbactions"/>                  
          </TD>
	  <TD>
            Nb passed :
            <xsl:value-of select="$nbpassed"/>                                     
          </TD>
	  <TD>
            Nb warn :
            <xsl:value-of select="$nbwarning"/>
          </TD>
	  <TD>
            Nb KOs :
            <xsl:value-of select="$nbko"/>
          </TD>
        </TR>
      </TBODY>
    </TABLE>
  </xsl:template>    
  <!-- ========================= -->
  <!-- child element: groups     -->
  <!-- ========================= -->
  <xsl:template match="groups">
    Groups :
    <BR/>
      <xsl:for-each select="group">
	<xsl:if test="position() != 1"> - </xsl:if>
	<xsl:value-of select="."/>
      </xsl:for-each>
      <BR/>
  </xsl:template> 

  <!-- ========================= -->
  <!-- child element: actions    -->
  <!-- ========================= -->
  <xsl:template match="actions">
    <xsl:apply-templates/>
  </xsl:template>

  <!-- ========================= -->
  <!-- child element: action     -->
  <!-- ========================= -->
  <xsl:template match="action">
    <TABLE class="results table table-bordered table-hover">
      <THEAD>
	<TR>
	  <TH class="w-75 p-3">
            Action :
            <xsl:value-of select="line"/>            
	  </TH>
	  <xsl:choose>
            <xsl:when test="passed = 'true'">
	      <TH class="test-passed w-25 p-3" STYLE="text-align: center;">
		<IMG src="true.png"/>
	      </TH>
            </xsl:when>
            <xsl:when test="passed = 'warning'">
	      <TH class="test-warn w-25 p-3" STYLE="text-align: center;">
		<IMG src="warning.png"/>
	      </TH>
            </xsl:when>
            <xsl:when test="passed = 'false'">
	      <TH class="test-fail w-25 p-3" STYLE="text-align: center;">
		<IMG src="false.png"/>
	      </TH>
            </xsl:when>
          </xsl:choose>
	</TR>
	<TR>
	  <TD class="w-100 p-3" COLSPAN="2">
	    <xsl:variable name="img" select="img/@src"/>
	    <IMG SRC="{$img}"/>
	  </TD>
	</TR>
	<TR>
	  <TD class="w-100 p-3" COLSPAN="2">
            Channel name :
	    <xsl:value-of select="channel/@name"/>
	  </TD>
	</TR>
	<TR>
	  <TD class="w-100 p-3" COLSPAN="2">
            Timeline :
	    <xsl:value-of select="timeline"/>            
	  </TD>
	</TR>
	<TR>
	  <TD class="w-100 p-3" COLSPAN="2">
            Action type :
            <xsl:value-of select="@type"/>            
	  </TD>
	</TR>
	<TR>
	  <TD class="w-100 p-3" COLSPAN="2">
            Element tag :
            <xsl:value-of select="element/@tag"/>            
	  </TD>
	</TR>
	<TR>
	  <TD class="w-100 p-3" COLSPAN="2">
            Criterias :
            <xsl:value-of select="element/criterias"/>            
	  </TD>
	</TR>
      </THEAD>
    </TABLE>
  </xsl:template>
</xsl:stylesheet>


